(define-library (sunny quasiquote)
    (export quasiquote)
    (import (sunny core))
    (begin
      ; I found this definition on the internet
      ; https://groups.google.com/forum/#!topic/comp.lang.scheme/koHP9xzPF3Y
      (define-syntax quasiquote
        (syntax-rules (quasiquote unquote unquote-splicing quote)
          ((quasiquote a)
           (quasiquote "zero" a))
          ((quasiquote l (quote a))
           (list 'quote (quasiquote  l a)))
          ((quasiquote "zero" (unquote a))
           a)
          ((quasiquote "zero" b . (unquote a))
           (cons b a))
          ((quasiquote ("succ" . l) (unquote a))
           (list 'unquote (quasiquote l a)))
          ((quasiquote l (unquote-splicing a))
           (syntax-error "unquote-splicing"))
          ((quasiquote "zero" ((unquote-splicing a) . r))
           (append a (quasiquote  "zero" r)))
          ((quasiquote ("succ" . l) ((unquote-splicing a) . r))
           (cons (list 'unquote-splicing (quasiquote l a))
                 (quasiquote ("succ" . l) r)))
          ((quasiquote l (quasiquote a))
           (list 'quasiquote (quasiquote ("succ" . l) a)))

          ((quasiquote l (r . r1))
           (cons (quasiquote l r) (quasiquote l r1)))
          ((quasiquote l #(r ...))
           (list->vector (quasiquote  l (r ...))))
          ((quasiquote l a)
           (quote a))))))
